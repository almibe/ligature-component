@precedence { high }

@top Script { list<Expression> }

single { Name | Field | !high Bool | !high Int | String | Module | Array | Grouping | When | Lambda }
Expression { single | Application }

String { !high string }
Module { !high "{" list<Field | Name>? "}" }
Array  { !high "[" list<Expression>? "]" }
Grouping { !high "(" list<Expression>? ")" }
When { !high "when" list<(Expression "=>" Expression ",")> "end" }
Lambda { !high "\\" Name+ "->" Expression }
Application { Name single+ }

Field { !high Name "=" Expression }

@tokens {
  Bool { "true" | "false" }
  Int { '-'? int }
  int  { '0' | $[1-9] @digit* }
  Comment[isolate] { "--" ![\n]* }

  Name { $[a-zA-Z_]$[\.0-9a-zA-Z-_]* }
  string { 'i'? '"' char* '"' }
  char { $[\u{20}\u{21}\u{23}-\u{5b}\u{5d}-\u{10ffff}] | "\\" esc }
  esc  { $["\\\/bfnrt] | "u" hex hex hex hex }
  hex  { $[0-9a-fA-F] }

  whitespace { $[ \n\r\t] }

  @precedence { "when", "end", Bool, "->", string, Name }
}

@skip { whitespace | Comment }
list<item> { item ("," item)* ","? }

@detectDelim